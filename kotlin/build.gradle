plugins {
    id "com.google.protobuf" version "0.8.10"
    id "java"
}

repositories {
    mavenCentral()
}

version '1.0.0-SNAPSHOT'

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.google.protobuf'

    repositories {
        mavenCentral()
        jcenter()
    }

    version = project.version

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:3.10.0"
        }
        generateProtoTasks {
            configure(all()) {
                doLast {
                    outputs.files.forEach { dir ->
                        ant.replace(dir: dir, token: "OuterClass", value: "Proto")
                        ant.move(todir: dir) {
                            ant.fileset(dir: dir)
                            ant.mapper(type: 'glob', from: '*OuterClass.java', to: '*Proto.java')
                        }
                    }
                }
            }
        }
        generatedFilesBaseDir = "$projectDir/src"
    }


    dependencies {
        implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.10.0'
    }
}

